class Rhino : ZilchComponent
{
  // Dependencies
  [Dependency] var Character : Character;
  [Dependency] var SweptController : SweptController;
  [Dependency] var AnimationController : AnimationController;
  
  // Easy access to the players position
  var PlayerPosition : Real3
  {
    get
    {
      return this.Space.FindObjectByName("Player").Transform.Translation;
    }
  }
  
  var PlayerIsDead : Boolean
  {
    get
    {
      return this.Space.FindObjectByName("Player").Character.Dead;
    }
  }
  
  var Charging : Boolean
  {
    // We aren't charging if we're attacking
    get { return (this.Character.MovementState == MovementState.Running) && !this.Attacking;}
  }
  
  var Attacking : Boolean
  {
    get { return this.Owner.AnimationController.Attacking; }
  }
  
  // Easy access to our position
  var OurPosition : Real3
  {
    get { return this.Owner.Transform.Translation; }
  }
  
  var Dead : Boolean
  {
    get { return this.Character.Dead;}
  }
  
  var OnGround : Boolean
  {
    get { return this.Owner.SweptController.Grounded;}
  }
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    // Camera shake when the rhino is running
    var vel = Math.Length(this.Owner.RigidBody.Velocity);
    if(vel >= 5)
    {
      if(this.OnGround)
      {
        var camera = this.Space.FindObjectByName("Camera");
        camera.CameraShake.CurrentIntensity = 0.0001;
      }
    }
  }
  
  function Jump()
  {
    this.Character.Jump();
  }
  
  function WalkLeft()
  {
    this.Character.WalkLeft();
  }
  
  function WalkRight()
  {
    this.Character.WalkRight();
  }
  
  function ChargeLeft()
  {
    this.Character.RunLeft();
  }
  
  function ChargeRight()
  {
    this.Character.RunRight();
  }
  
  function Attack()
  {
    if(!this.Attacking)
    {
      this.Owner.AnimationController.Attack();
    }
  }
}
